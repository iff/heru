#!/usr/bin/env coffee

{ join } = require 'path'
{ realpathSync } = require 'fs'
{ inspect } = require 'sys'

# Path to the runtime directory.
runtime = realpathSync join __dirname, '../';

# Add directories with the runtime files to the require path.
require.paths.unshift join runtime, 'src'
require.paths.unshift join runtime, 'vendor'

# The colors module extends the String prototype, it does not export anything.
require 'colors'

# Load the main heru module.
heru = require 'heru'
console.log "Heru #{ heru.Version.yellow }"

# Hack. We need to have a better way to figure out where the library is.
require.paths.unshift(process.env.PWD + '/library')

# Load the configuration for the given node.
loadNode = (hostname) ->
  path = hostname.split('.').reverse()
  require join process.env.PWD, 'nodes', path.join('/')

# There is no way to get the FQDN in node. The user needs to supply the
# hostname on the commandline.
spec = loadNode process.argv[process.argv.length - 1]
node = new heru.Node process.argv[process.argv.length - 1], spec

ind = (indent) ->
  a = []
  while indent--
    a.push '  '
  return a.join('')

dumpError = (err, indent = 0) ->
  console.log "#{ind(indent)}#{err.message}" if err
  return unless err.children

  for err in err.children
    dumpError err, indent + 1


# This is where it comes all together. First initialize the node. This gives
# it a chance to load manifests, resolve resources etc.
node.init().when (err) ->
  if err
    return dumpError err

  # Everything looks fine so far, now verify that the node is in order.
  # Ideally, this step would not perform any changes on the system. In reality
  # it runs certain write operations. Somebody should fix that.
  node.verify().when (err) ->
    if err
      if '--dry-run' in process.argv
        console.log "dry run mode"
        dumpError err
      else
        console.log 'Node is in incomplete state. Taking corrective measures.'
        dumpError err
        node.amend().when (err) ->
          dumpError err if err
    else
      console.log 'Node verified. Nothing to do'

