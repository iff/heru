#!/usr/bin/env coffee

{ join } = require 'path'
{ realpathSync } = require 'fs'
{ inspect } = require 'sys'

# Path to the runtime directory.
runtime = realpathSync join __dirname, '../';

# Add directories with the runtime files to the require path.
require.paths.unshift join runtime, 'src'
require.paths.unshift join runtime, 'vendor'

# The colors module extends the String prototype, it does not export anything.
require 'colors'

# Load the main heru module.
heru = require 'heru'
console.log "Heru #{ heru.Version.yellow }"

# Hack. We need to have a better way to figure out where the library is.
require.paths.unshift(process.env.PWD + '/library')

# Load the configuration for the given node.
loadNode = (hostname) ->
  path = hostname.split('.').reverse()
  require join process.env.PWD, 'nodes', path.join('/')

# There is no way to get the FQDN in node. The user needs to supply the
# hostname on the commandline.
spec = loadNode process.argv[2]
node = new heru.Node process.argv[2], spec

ind = (indent) ->
  a = []
  while indent--
    a.push '  '
  return a.join('')

dumpError = (err, indent = 0) ->
  console.log "#{ind(indent)}#{err.message}" if err
  return unless err.children

  for err in err.children
    dumpError err, indent + 1

futureChain = (obj, method) ->
  args = Array.prototype.slice.call arguments

  future = obj[method]()
  future.when (err) ->
    if err
      dumpError err
    else if args[2]
      process.nextTick ->
        args = args.slice 2
        args.unshift obj
        futureChain.apply @, args

# Try to apply the node configuration, catch any errors which may be thrown.
try
  futureChain node, 'bootstrap', 'verify', 'amend'

catch error
  console.log "Got an error:".red
  console.log error
  console.log error.stack

